dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            outputDirectory.set(buildDir.resolve("dokka"))
            includes.from(files("README.md"))
        }
    }
}

task docJar(type: Jar, dependsOn: dokkaHtml) {
    archiveClassifier.set("javadoc")
    from dokkaHtml.outputDirectory
}

task sourceJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact sourceJar
                artifact docJar

                groupId = POM_GROUP_ID
                artifactId = POM_ARTIFACT_ID
                version = POM_VERSION
            }
        }
    }
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintray {
        user = properties.getProperty('BINTRAY_USER')
        key = properties.getProperty('BINTRAY_API_KEY')
        publications = ['release']
        configurations = ['archives']
        override = true
        pkg {
            repo = POM_REPO
            userOrg = POM_PKG_ORG
            name = POM_PKG_NAME
            description = POM_DESCRIPTION
            publish = true
            publicDownloadNumbers = true
            licenses = [POM_LICENSE_NAME]
            vcsUrl = POM_VSC_URL
            dryRun = false
            version {
                name = POM_VERSION
                released = new Date()
                vcsTag = POM_VERSION
            }
        }
    }
}
